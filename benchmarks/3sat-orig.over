// Lambda Expressions vs. Anonymous Methods, Part Five
//   by Eric Lippert
// https://learn.microsoft.com/en-us/archive/blogs/ericlippert/lambda-expressions-vs-anonymous-methods-part-five
// Encoding 3-SAT using overloading!

assume orTTT: (T, T, T) => T;
assume orTTF: (T, T, F) => T;
assume orTFT: (T, F, T) => T;
assume orFTT: (F, T, T) => T;
assume orTFF: (T, F, F) => T;
assume orFTF: (F, T, F) => T;
assume orFFT: (F, F, T) => T;
assume orFFF: (F, F, F) => F;
assume or = <orTTT, orTTF, orTFT, orFTT, orTFF, orFTF, orFFT, orFFF>;

assume andTT: (T, T) => T;
assume andTF: (T, F) => F;
assume andFT: (F, T) => F;
assume andFF: (F, F) => F;
assume and = <andTT, andTF, andFT, andFF>;

assume notT: T => F;
assume notF: F => T;
assume not = <notT, notF>;

assume mustBeTrue: T => Unit;

assume MT: (T => Unit) => Unit;
assume MF: (F => Unit) => Unit;
assume M = <MT, MF>;

// (!x3) & ((!x1) & ((x1 | x2 | x1) & (x2 | x3 | x2)))
M(λ(x1) => M(λ(x2) => M(λ(x3) =>
  mustBeTrue(
    and(not(x3),
      and(not(x1),
        and(
          or(x1, x2, x1),
          or(x2, x3, x2)))))
)))
